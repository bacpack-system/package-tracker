##
# Test: NO_DEBUG=ON skips debug suffix and build type keywords for executable
#
# Verifies that BA_PACKAGE_EXECUTABLE with NO_DEBUG=ON does not add DEBUG/RELEASE
# keywords and does not append 'd' suffix even in Debug build mode.
#
IF(NOT DEFINED CMAKE_SCRIPT_MODE_FILE)
    CMAKE_MINIMUM_REQUIRED(VERSION 3.21)
    PROJECT(TEST_EXEC_NO_DEBUG_SKIPS_SUFFIX)
ENDIF()

FIND_PACKAGE(CMLIB REQUIRED)

SET(MOCK_CMLIB_STORAGE_TEMPLATE_INSTANCE_OUTPUT "https://example.com/main/mock_distro/0.0.0/mock_arch/mypkg/mypkg_v1.0.0_mock_arch_mock_distro_0.0.0.zip" CACHE INTERNAL "")
INCLUDE("${CMAKE_CURRENT_LIST_DIR}/../../include_general.cmake")

SET(CMAKE_BUILD_TYPE "Debug")
BA_PACKAGE_VARS_SET(REVISION "main")
BA_PACKAGE_VARS_SET(URI_TEMPLATE "https://example.com/<REVISION>/<GIT_PATH>/<PACKAGE_GROUP_NAME>/<ARCHIVE_NAME>")
BA_PACKAGE_VARS_SET(GIT_PATH_TEMPLATE "")

BA_PACKAGE_EXECUTABLE(mypkg v1.0.0 NO_DEBUG ON)

TEST_VAR_TRUE(MOCK_CMLIB_DEPENDENCY_CALLED)

STRING(FIND "${MOCK_CMLIB_DEPENDENCY_URI}" "mypkgd" found_debug_suffix)
IF(NOT found_debug_suffix EQUAL -1)
    MESSAGE(FATAL_ERROR "URI should NOT contain 'd' suffix when NO_DEBUG=ON: ${MOCK_CMLIB_DEPENDENCY_URI}")
ENDIF()

STRING(FIND "${MOCK_CMLIB_DEPENDENCY_URI}" "mypkg_v1.0.0" found_plain)
IF(found_plain EQUAL -1)
    MESSAGE(FATAL_ERROR "URI should contain 'mypkg_v1.0.0' (no 'd'): ${MOCK_CMLIB_DEPENDENCY_URI}")
ENDIF()

LIST(FIND MOCK_CMLIB_DEPENDENCY_KEYWORDS "DEBUG" idx_debug)
IF(NOT idx_debug EQUAL -1)
    MESSAGE(FATAL_ERROR "KEYWORDS should NOT contain DEBUG when NO_DEBUG=ON: ${MOCK_CMLIB_DEPENDENCY_KEYWORDS}")
ENDIF()

LIST(FIND MOCK_CMLIB_DEPENDENCY_KEYWORDS "RELEASE" idx_release)
IF(NOT idx_release EQUAL -1)
    MESSAGE(FATAL_ERROR "KEYWORDS should NOT contain RELEASE when NO_DEBUG=ON: ${MOCK_CMLIB_DEPENDENCY_KEYWORDS}")
ENDIF()

